1 SCREEN 1
5 A9 = 64 : A8 = 20
9 DIM NA$(A9+1), A0(A9+1), CH(A9+1,5), UP$(A9+1)
10 DIM TM(4) :REM ONE TEAM OF 4 MEMBERS
11 DIM TE(A9+1) :REM TERMS
20 A0$(0) = "DEAD"
25 A0$(1) = "OK  "

20 REM <------------------------------------- SOPHONTS (S)
21 DIM S1$(16), S2$(16), S(A9+1)
30 FOR X = 0 TO 15 :READ S1$(X),S2$(X) :NEXT
32 Da Q,CETIAN  , C,CHAMAXI , R,DARRIAN , L,LLELLEWY
33 Da T,THREEP  , A,ASLAN   , I,VARGR   , V,"VILANI  "
38 Da S,SOLOMANI, B,BWAP    , M,AMINDII , D,"DROYNE  "
39 Da J,SMRII   , V,VILANI  , Y,AEL YAEL, E,SHRIEKER 
50 REM <------------------------------------- CAREERS (C)
51 DIM C1$(8), C2$(8), C(A9+1)
55 FOR X = 0 TO 7 :READ C1$(X), C2$(X) :NEXT
60 Da A,SOLDIER ,  R,MARINE  ,  G,AGENT   ,  O,"ROGUE   "
70 Da N,SPACER  ,  S,SCOUT   ,  M,MERCHANT,  L,"SCHOLAR "
100 REM <------------------------------------ RANKS (R)
101 DIM R$(8), R(A9+1)
105 FOR X = 0 TO 7 :READ R$(X) :NEXT
110 DATA E1,E2,E3,E4,O1,O2,O3,O4
184 REM <------------------------------------ SKILLS (K)
185 SK=15
186 DIM K$(SK+1), K1$(SK+1), K(A9,SK+1),KK$(A9)
187 FOR X = 0 TO SK :READ K1$(X), K$(X) :NEXT
190 Da H,HOSTILE ENV, U,SURVIVAL,   F,FIGHTER,  G,GUNNER
191 Da N,STRATEGY,    W,STREETWISE, M,MEDIC,    A,ASTROGATOR
192 Da E,ENGINEER,    P,PILOT,      S,STEWARD,  V,VACC
193 Da T,TRADER,      R,SURVEY,     D,DIPLOMAT, B,BUREAUCRAT
209 REM <------------------------------------ EHEX
210 DIM HX$(34)
211 FOR X = 0 TO 33 :READ HX$(X) :NEXT
215 DATA "0",1,2,3,4,5,6,7,8,9,A,B,C,D,E,F,G,H,J,K,L,M,N,P,Q,R,S,T,U,V,W,X,Y,Z
220 INPUT "PRESS A KEY TO BEGIN"; K$

224 REM <------------------------------------ FILL REGISTRY
225 X=RND(-TI)
230 FOR SE = 1 TO A9
232    REM EN=0:GOSUB 3000 :REM CHECK IF FILE EXISTS
237    REM IF EN=0 THEN GOSUB 3100 :GOTO 240 :REM LOAD FILE
238    GOSUB 605 :REM GENERATE NEW ENTRY
239    GOSUB 1000 :REM SAVE ENTRY
240 NEXT
245 REM MAIN LOOP
249 REM 1=WHITE,3=AQUA,5=GREEN,7=YELLOW,13-15 ALL OK
250 COLOR 3,0
255 ? CHR$(147) SPC(30) "CHARACTER REGISTER"
265 FOR P = 1 TO A8
270    IF INT(P/2) <> P/2 THEN ?
275    PT$=" ":FOR I=1 TO 4:IF TM(I)=P THEN PT$="*":
276    NEXT I
280    COLOR 3 : ? " " CHR$(P+64);   :REM MID$(STR$(P)+"   ",2,3);
285    COLOR 1 : ? PT$;
290    COLOR 5 : ? A0$(A0(P));
300    COLOR 14 : ? " " C2$(C(P));
305    COLOR 15 : ? " " S1$(S(P)) ":" KK$(P);
310    COLOR 7 : ? " " NA$(P);
320 NEXT P
330 ?:?:? "     (A-T) VIEW  (U) FORM PARTY  (V) DISBAND  (* = IN PARTY)"
331 GET A$ :IF A$<"A" OR A$ > "V" GOTO 331
332 SE = ASC(A$)-64
333 IF A$="U" THEN GOSUB 2000 :GOTO 245
334 IF A$="V" THEN GOSUB 2100 :GOTO 245
445 ?
450 COLOR 5,0 :REM GREEN ON BLACK
455 ? SPC(5) "NAME:   " NA$(SE);
460 ? SPC(10) "(" A0$(A0(SE)) " " S2$(S(SE)) ")"
465 ? SPC(5) "CAREER: " C2$(C(SE)) "," TE(SE) "TERMS, RANK " R$(R(SE))
470 ? SPC(5) "UPP:    "; UP$(SE)
490 ?:? SPC(5) "SKILLS: "
495 Y = 0 :CR = 0
500 FOR X = 0 TO SK
505    IF K(SE,X) = 0 GOTO 530
510    IF Y/2 = INT(Y/2)  THEN ? SPC(20) K(SE,X) LEFT$(K$(X)+"          ",16),,
515    IF Y/2 <> INT(Y/2) THEN ? K(SE,X) K$(X)
520    Y=Y+1
525    CR=CR+K(SE,X)
530 NEXT X
535 ? SPC(5) "SALARY: CR " CR * 100
540 ?:?
545 COLOR 7,0 :REM YELLOW ON BLACK
550 ? SPC(5) "(D)ELETE (K)EEP (R)ENAME?"
555 GET K$ 
560 IF K$ = "K" AND NA$(SE) =  "-           " GOTO 585
561 IF K$ = "K" AND NA$(SE) <> "-           " GOTO 245
565 IF K$ = "D" THEN GOSUB 605 :GOSUB 1000   :GOTO 245
575 IF K$ = "R" GOTO 585
580 GOTO 555
585 INPUT "     ENTER NAME"; NA$(SE)
586 IF NA$(SE) = "" THEN NA$(SE) = "-           "
587 NA$(SE) = LEFT$(NA$(SE) + "           ", 12)
588 GOSUB 1000 :REM SAVE ENTRY
590 GOTO 245
595 END

600 REM <------------------------ SUBROUTINE: GENERATE NEW ENTRY
601 "
602 "INPUT: SE (THE SELECTED CHARACTER SLOT NUMBER)
603 "
604 " <---------------------------------------------------------
605 NA$(SE) = "-           "
606 UP$(SE) = "" :REM CLEAR UPP
610 A0(SE) = 1 :REM OK
615 S(SE) = INT(RND(1)*9)+INT(RND(1)*8) :REM 16 TYPES
620 REM C(SE) = INT(RND(1)*8) :REM CAREER 0-7 (MOVED TO AFTER)
625 FOR X = 0 TO 5 
630    N = INT(RND(1)*9)+INT(RND(1)*8) :REM 0 THRU 15
632    CH(SE,X) = N
633    UP$(SE)=UP$(SE)+HX$( INT(N*11/16)+2 ) :REM TO UPP (2-12)
635 NEXT X
639 REM DETERMINISTIC CAREER, TERMS, RANK, AND SKILLS
640 FOR X = 0 TO 15: K(SE,X)=0 :NEXT X
641 KK$(SE) = ""
645 REM BUILD RANDOM SEED, SLOPPILY
650 RR =      CH(SE,0)              :REM 0-F
655 RR = RR + CH(SE,1) *  $10       :REM 0-F
660 RR = RR + CH(SE,2) *  $100      :REM 0-F
665 RR = RR + CH(SE,3) *  $1000     :REM 0-F
670 RR = RR + CH(SE,4) *  $10000    :REM 0-F
675 RR = RR + CH(SE,5) *  $100000   :REM 0-F
680 RR = RR + S(SE)    *  $1000000  :REM 16 SOPHONT TYPES
685 REM RR = RR + C(SE)    * $10000000
690 X=RND(-RR)
691 C(SE) = INT(RND(1)*8) :REM CAREER 0-7
695 TE = INT(RND(1)*6)+1  :REM TERMS  1-6
697 R = INT(RND(1)*TE)    :REM RANK   1-6
700 TE(SE) = TE
701 R(SE) = R
705 REM <------------------------------------- ASSIGN SKILLS
706 REM 
707 REM      SELECTION POOL: CAREER NUMBER + 2D5
708 REM
715 REM <---------------------------------------------------
720 K = TE * 2 + INT(R / 2)
721 M = 0
725 FOR X = 0 TO K
730    N = INT(RND(1)*5)+INT(RND(1)*5)+C(SE) :REM 0 TO 15!
731    IF K(SE,N) = 0 THEN M=M+1 : KK$(SE) = KK$(SE)+K1$(N)
735    K(SE,N) = K(SE,N) + 1 : REM INT(RND(1)*4+1)
737    IF M = 6 THEN 750 :REM DONE
740 NEXT X
750 KK$(SE) = LEFT$(KK$(SE)+"        ", 6)
790 RETURN
791 REM ------------------------> SUBROUTINE: GENERATE NEW ENTRY
792 "
793 " SUBROUTINE COMPLETED.
794 "
795 " --------------------------------------------------------->

1000 REM -----------------------> SUBROUTINE: WRITE REGISTRY
1010 X = SE
1015    F$ = "REG" + MID$(STR$(X)+"   ",2,3)
1020    OPEN2,8,2,"@:" + F$ + ",S,W"
1021    PRINT#2, X;
1022    IF X<10 THEN PRINT#2, " ";
1025    PRINT#2, S(X);
1026    IF S(X)<10 THEN PRINT#2, " ";
1030    FOR Y = 0 TO 5
1032       PRINT#2, CH(X,Y);
1033       IF CH(X,Y)<10 THEN PRINT#2," ";
1035    NEXT Y
1037    PRINT#2, NA$(X) "," A0(X) "," S2$(S(X)) ",";
1039    PRINT#2, C2$(C(X)) "," HX$(TE(X)) "," HX$(R(X)) "," UP$(X) ",";
1050    SS=0
1051    FOR Y = 0 TO SK
1052       IF K(X,Y) = 0 OR SS > 4 THEN 1055
1053       PRINT#2, K1$(Y) "," HX$(K(X,Y)) ",";
1054       SS=SS+1
1055    NEXT Y
1056    IF SS<6 THEN FOR Y = SS TO 5 :PRINT#2, "0,0,"; :NEXT Y
1057    PRINT#2, "0"
1060    PRINT#2, "       "; :REM 96 BYTES
1065    PRINT#2, "                                " :REM 128 BYTES
1069    CLOSE 2
1100 RETURN

2000 REM --- CREATE PARTY ---
2001 ? "SELECT 4 CHARACTERS FOR YOUR PARTY"
2002 FOR I=1 TO 4
2020   ? "CHARACTER # " I "(A-T)"
2025   GET CN$: IF CN$<"A" OR CN$>"T" THEN GOTO 2025
2060   FD=0: CN=ASC(CN$)-64
2065   FOR J=1 TO 4
2070     IF TM(J)=CN THEN FD=1
2075   NEXT J
2080   IF FD=1 THEN ? "ALREADY IN PARTY!" :GOTO 2020
2085   TM(I)=CN
2090 NEXT I
2099 RETURN

2100 REM --- DISBAND PARTY ---
2101 ? "DISBANDING PARTY..."
2102 FOR I=1 TO 4
2103   TM(I)=0
2104 NEXT I
2105 RETURN

3000 REM --- CHECK IF FILE EXISTS ---
3001 REM SE IS THE CURRENT SLOT
3010 F$ = "REG" + MID$(STR$(SE)+"   ",2,3)
3020 OPEN 15,8,15,"I0"
3030 OPEN 2,8,2,F$ + ",S,R"
3040 INPUT#15,EN,EM$
3050 CLOSE 2
3060 CLOSE 15
3070 ? EN, EM$
3099 RETURN

3100 REM --- LOAD EXISTING ENTRY ---
3101 REM SE IS THE CURRENT SLOT
3110 F$ = "REG" + MID$(STR$(SE)+"   ",2,3)
3120 OPEN 2,8,2,F$ + ",S,R"
3130 INPUT#2, X
3135 INPUT#2, S(SE)
3140 FOR Y = 0 TO 5
3145    INPUT#2, CH(SE,Y)
3150 NEXT Y
3155 INPUT#2, NA$(SE) A0(SE) ":" S2$(S(SE)) ":";
3160 INPUT#2, C2$(C(SE)) ":" HX$(TE(SE)) HX$(R(SE)) ":";UP$(SE);":";
3170 REM THE REST IS GENERATED FROM ABOVE
3199 RETURN

