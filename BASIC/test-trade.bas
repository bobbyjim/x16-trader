5 DIM GS$(20), PR(20), SH(20), SP(20), MU(20)
10 PC=0
15 READ G$,PR
20    GS$(PC) = G$ 
25    PR(PC) = PR
30    PC=PC+1
35    READ G$,PR
40    IF G$ <> "-1" THEN 20
45 DATA AG NI RI, 8, BA DE PO, 1,  DE HI   ,5
50 DATA FL NI   , 4, HI IN   , 9,  LO VA   ,4
55 DATA NA NI PO, 4, NI RI   , 8
60 DATA -1,-1
65 PC=PC-1
70 INPUT "PRESS <ENTER>"; EE$ :X=RND(-TI)
75 Y=0
80 FOR X = 0 TO PC
85    SP(X) = INT(RND(1)*20+1)
90    MU(X) = 1
95    IF RND(1) > 0.5 THEN MU(X) = 5
100    IF RND(1) > 0.5 THEN MU(X) = 10
105 NEXT X
110 CR = 1000
115 ER$ = "        \X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
120 WL$ = ""
125 FOR X = 0 TO 79: WL$=WL$+CHR$(195) :NEXT
130 RL$ = CHR$(28) + WL$ + CHR$(155)
135 REM 155 = LT GREY
140 COLOR 15,0
145 ?CHR$(147) 
150 ?CHR$(19)
155 ? "CURSOR UP/DOWN TO NAVIGATE, LEFT/RIGHT TO BUY/SELL"
160 ? "\X11"
165 ? "TYPE 'B' TO BUY ALL OF A COMMODITY"
170 ? "TYPE 'S' TO SELL ALL OF A COMMODITY"
175 ? WL$
180 ? "\X11\X11" SPC(10) "CREDITS: " ER$; CR * 100 "\X11\X11"
185 ? RL$
190 ? SPC(10) "T ON",  "        ", " LOTS AT",  " LOT" 
195 ? SPC(10) "BOARD", "LOT TYPE", " STARPORT", " SIZE", "KCR/T\X11"
200 ? RL$
205 FOR X = 0 TO PC
210    ? SPC(10);
215    IF X=Y THEN ? ER$;
220    ? SH(X) * MU(Y),
225    IF X=Y THEN ? "\X12" ;
230    ? GS$(X),
235    ? "\X92" ER$ "  " SP(X), MU(X), PR(X) "\X11"
240 NEXT X
245 X=0
250 GET A$: IF A$="" GOTO 245
255 IF A$ = "\X11" AND Y < PC THEN Y=Y+1
260 IF A$ = "\X91" AND Y > 0 THEN Y=Y-1
265 IF A$ = "\X9D" THEN X=-1
270 IF A$ = "\X1D" THEN X=+1
275 IF A$ = "B" THEN X=-2
280 IF A$ = "S" THEN X=2
285 C1 = 0
290 C = PR(Y) * MU(Y) * 10
295 CA = C * SP(Y)
300 CB = C * SH(Y)
305 IF X=-2 AND SP(Y)>0 AND CR>=CA THEN SH(Y)=SH(Y)+SP(Y):SP(Y)=0:C1=-CA
310 IF X=-1 AND SP(Y)>0 AND CR>=C THEN SP(Y)=SP(Y)-1 :SH(Y)=SH(Y)+1 :C1=-C
315 IF X=1  AND SH(Y)>0 THEN SP(Y)=SP(Y)+1 :SH(Y)=SH(Y)-1 :C1=C
320 IF X=2  THEN SP(Y)=SP(Y)+SH(Y):SH(Y)=0:C1=CB
325 CR=CR+C1
330 GOTO 150
