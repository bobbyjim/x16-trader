5 ?CHR$(147)
10 ? "GUNER1"
15 ?
20 ? "PRESS ANY KEY"
25 GET A$ :IF A$="" GOTO 25
30 V=RND(-TI)
35 S=INT(RND(1)*100+1)   :REM SPEED
40 D=INT(RND(1)*10000+1) :REM DISTANCE
45 M=100000-75000*RND(1) :REM MAX GUN RANGE
50 ? "SPEED:       "S" KM/S"
55 ? "KILL RADUIS: "D
60 FOR K=1 TO M/10000
65 K1=RND(1)
70 NEXT K
75 R=INT(.95*M-.6*M*RND(1))
80 N=0 :GOTO 90
85 IF R>M THEN ? "* TARGET OUT OF RANGE *" :GOTO 195
90 ? "TARGET RANGE IS"R"KILOMETERS"
95 INPUT "ELEVATION"; E
100 IF E>0 AND E<90 GOTO 120
105 IF E>89 THEN ? "MAX IS 89 DEGREES."
110 IF E<1  THEN ? "MIN IS 1 DEGREE." 
115 GOTO 95
120 N=N+1
125 K=INT(R-M*SIN(2*E/57.3))
130 K1=ABS(K)
135 IF K1<D GOTO 185
140 REM HANDLE SHORT OR LONG
145 IF K>D  THEN ? "SHORT OF TARGET BY"K1"YARDS"
150 IF K<-D THEN ? "OVER TARGET BY"K1"YARDS"
155 C=INT(2*RND(1)+1)
160 IF C<>1 THEN C=-1
165 C1=M*S/100*RND(1)
170 C1=C*C1
175 R=R+C1
180 GOTO 85
185 ? "*** TARGET DESTROYED ***"
190 GOSUB 220
195 D1=K
200 FOR K=1 TO N+D/100
205 K1 = RND(1)
210 NEXT K
215 GOTO 45
220 IF N=1 THEN PRINT "***** DIRECT HIT *****" 
225 IF N>1 THEN PRINT N "ROUNDS EXPENDED"
230 RETURN
