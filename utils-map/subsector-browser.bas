5 LOAD "SS-502-500.BIN",8,15,$A000
10 AD=$A100 :REM SYSTEM DATA STARTS HERE
15 POKE $9F61, 15 :REM MAP IS CENTERED ON BANK 15
20 FOR C = 1 TO 8
25 FOR R = 1 TO 10
30 REM ---------------------------------------
35 REM
40 REM  SYSTEMS ARE 48 BYTES LONG.
45 REM
50 REM ---------------------------------------
55 AD = $A100 + (R-1)*48 + (C-1)*480
60 IF PEEK(AD+6) = 0 GOTO 245 :REM SKIP EMPTY HEX
65 LX$ = "0" + MID$(STR$(PEEK(AD)),2)
70 IF PEEK(AD+1) > 9 THEN LX$ = LX$ + "10"
75 IF PEEK(AD+1) <10 THEN LX$ = LX$ + "0" + MID$(STR$(PEEK(AD+1)),2)
80 ? "   " LX$ " ";             :REM ROW, COL
85 CC = PEEK(AD+2) + PEEK(AD+3) * 256 :REM ABSOLUTE COL
90 RR = PEEK(AD+4) + PEEK(AD+5) * 256 :REM ABSOLUTE ROW
95 FOR X = 0 TO 14 : ? CHR$(PEEK(AD+6+X)); :NEXT
100 ? " ";
105 ? CHR$(PEEK(AD+21));    :REM STARPORT
110 ? "-";
115 ? CHR$(48+PEEK(AD+27)); :REM SIZ
120 ? CHR$(48+PEEK(AD+28)); :REM ATM
125 ? CHR$(48+PEEK(AD+29)); :REM HYD
130 ? CHR$(48+PEEK(AD+30)); :REM POP
135 ? CHR$(48+PEEK(AD+31)); :REM GOV
140 ? CHR$(48+PEEK(AD+32)); :REM LAW
145 ? "-";
150 ? CHR$(PEEK($A0C0 + PEEK(AD+33))) " ";  :REM TL
155 ? CHR$(PEEK(AD+22));    :REM BASE 1
160 ? CHR$(PEEK(AD+23));    :REM BASE 2
165 ? " ";
170 TC = PEEK(AD+34)        :REM TCSAN
175 T1 = PEEK(AD+35)        :REM TC1
180 ? PEEK(AD+38);          :REM BELTS
185 ? PEEK(AD+39);          :REM GGS
190 ? LEFT$(STR$(PEEK(AD+40))+"    ",5); :REM STAR HALF INDEX 1
195 ? LEFT$(STR$(PEEK(AD+41))+"    ",5); :REM STAR HALF INDEX 2
200 ? CHR$(PEEK(AD+25));    :REM ZONE
205 ? " ";
210 ? CHR$(PEEK(AD+26));    :REM ALLEGIANCE
215 ? " "; 
220 YR = PEEK(AD+42)+1900 :REM YEAR
225 MO = PEEK(AD+43) :REM MONTH
230 DA = PEEK(AD+44) :REM MDAY
235 DD$ = MID$(STR$(YR),2) + "-" + MID$(STR$(MO),2) + "-" + MID$(STR$(DA),2)
240 ? DD$
245 NEXT
250 NEXT
