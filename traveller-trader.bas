5 COLOR 15,0 :?CHR$(147)	:REM LT GREY ON BLACK
10 DIM BU$(30) :REM LINE BUFFER
15 DIM DX(16),DY(16),DC(16),DR(16) :REM DESTINATION VECTORS
20 LOAD "ALLSHIPS.BIN"      ,8, 3,$A000 :REM BANK 3
25 LOAD "BANNER.BIN"        ,8, 4,$A000 :REM BANK 4
30 LOAD "SPINWARD.SPACE.BIN",8,11,$A000 :REM BANK 11 AND UP
35 GOSUB 1190
40 A0 = 100000 :REM CR
45 A2 = 19         :REM COL
50 A3 = 10         :REM ROW
55 A4 = 2        :REM RANGE
60 A6 = 0    :REM DEST.AROW
65 A5 = 0    :REM DEST.ACOL
70    GOSUB 605
75    GOSUB 100
80    GOTO 70
85 REM
90 REM -------------------------- SYSTEM -----------------------
95 REM
100    COLOR 15,0 :REM WHITE ON BLACK
105    A1 = 1
110 REM
115 REM  THERE IS ONLY ONE EXIT OUT OF THE SYSTEM, AND THAT IS "STARPORT"
120 REM
125    ON A1 GOTO 135, 210, 215, 265, 315
130    GOTO 125
135 ? CHR$(147) "\X11\X11"
140 BA$ = "      INSYSTEM"
145 GOSUB 1215
150 ?:? SPC(25) "YOU ARE IN ORBIT AROUND ";
155 GOSUB 360
160 ?:?
165 ?:? SPC(25) "1 - LAND AT STARPORT"
170 ?:? SPC(25) "2 - WILDERNESS REFUELING"
175 ?:? SPC(25) "3 - SYSTEM SURVEY"
180 ?:? SPC(25) "4 - JUMP"
185 ?:?
190 GET DE$
195 IF DE$ < "1" OR DE$ > "4" GOTO 190
200 A1 = VAL(DE$)+1
205 GOTO 125
210 RETURN
215 ? CHR$(147) "\X11\X11"
220 BA$ = "      REFUEL"
225 GOSUB 1215
230 ?:? SPC(25) "<REFUEL OPTIONS>"
235 ?:? SPC(25) "X: RETURN"
240 ?:?
245 GET DE$
250    IF DE$="X" THEN A1 = 1
255    IF DE$ < "1" OR DE$ > "X" GOTO 245
260 GOTO 125
265 ? CHR$(147) "\X11\X11"
270 BA$ = "      SURVEY"
275 GOSUB 1215
280 ?:? SPC(25) "<SURVEY OPTIONS>"
285 ?:? SPC(25) "X: RETURN"
290 ?:?
295 GET DE$
300    IF DE$="X" THEN A1 = 1
305    IF DE$ < "1" OR DE$ > "X" GOTO 295
310 GOTO 125
315 ? CHR$(147) "\X11\X11"
320 BA$ = "      JUMP"
325 GOSUB 1215
330 A2 = A5 
335 A3 = A6
340 INPUT "PRESS <RETURN>"; DE$
345 A1 = 1
350 GOTO 125
355 REM MAP.WHERE AM I(COL,ROW)
360    REM FIRST POINT TO THE RIGHT BANK
365    BA = INT((A2-1)/4)
370    POKE $9F61,11+BA
375    REM NOW CALCULATE THE ACTUAL COLUMN
380    CA = A2 - 1
385    CA = 4*(CA/4-INT(CA/4))
390    AD = $A000 + CA * 32 * 64 + (A3-1) * 32 * 1
395    FOR X = 10 TO 24: ? CHR$(PEEK(AD+X)); :NEXT
400    A7 = PEEK(AD+27)
405 RETURN
410 REM  GET REGIONAL UWPS(COL,ROW,RNG)
415 POKE $9F61,11 :REM MAP
420 DW = 0
425 FOR CC = A2 - A4 TO A2 + A4
430 FOR RR = A3 - A4 TO A3 + A4
435 NEXT RR
440 NEXT CC
445    ? CHR$(147) "SHIP LIST\X11"
450    POKE $9F61,3  :REM SHIPS
455    AD = $A000
460    CT = 0
465    IF PEEK(AD) = 0 THEN 580
470    O% = PEEK(AD+28) :REM OWNER
475    L = PEEK(AD+3)   :REM COMPONENT LENGTH
480    T = PEEK(AD+24)  :REM SIZE CODE
485    M1 = PEEK(AD+22) :REM MISSION CODE 1
490    IF A7 <> O% GOTO 570
495    IF M1 = ASC("E") GOTO 570 :REM NO ESCORTS
500    IF M1 = ASC("C") GOTO 570 :REM NO CRUISERS
505    ? "     ";
510    ? CHR$(M1);          :REM MISSION CODE 1
515    ? CHR$(PEEK(AD+23)); :REM MISSION CODE 2
520    ? " "; 
525    FOR X = 4 TO 21 : ? CHR$(PEEK(AD+X)); :NEXT
530    ? T*100;
535    IF T<10  THEN ? " ";
540    ? "TONS";
545    MC = PEEK(AD+27)
550    ? "  MCR " MC*T
555    CT=CT+1
560    IF CT/4 <> INT(CT/4) GOTO 570
565    ? "     -----------------------------------------"
570    AD = AD + 4 + L :REM NEXT RECORD
575    GOTO 465
580    IF CT = 0 THEN ? "     NO SHIPS AVAILABLE."
585    RETURN
590 REM
595 REM -------------------------- STARPORT -----------------------
600 REM
605    COLOR 15,0 :REM WHITE ON BLACK
610    A1 = 1
615 REM 
620 REM THERE IS ONLY ONE EXIT OUT OF THE STARPORT, AND THAT IS "EXIT STARPORT"
625 REM
630    ON A1 GOTO 645, 745, 970, 1020, 1075, 1120, 1155, 640
635    GOTO 630
640    RETURN
645 ? CHR$(147) "\X11\X11"
650 BA$ = "  KIGUKI"
655 GOSUB 1215
660 ?:? SPC(25) "WELCOME TO ";
665 GOSUB 360
670 ?:?
675 ?:? SPC(25) "1 - FILE FLIGHT PLAN";
680 IF A5 > 0 THEN ? " (FILED)"
685 IF A5 = 0 THEN ?
690 ?:? SPC(25) "2 - COMMODITY EXCHANGE"
695 ?:? SPC(25) "3 - SHIPYARD"
700 ?:? SPC(25) "4 - HIRING HALL"
705 ?:? SPC(25) "5 - SCOUT BASE"
710 ?:? SPC(25) "6 - NAVAL BASE"
715 ?:? SPC(25) "7 - EXIT STARPORT"
720 ?:?
725 GET DE$
730 IF DE$ < "1" OR DE$ > "7" GOTO 725
735 A1 = VAL(DE$)+1
740 GOTO 630
745 ? CHR$(147) "\X11\X11"
750 BA$ = "  GIMGU"
755 GOSUB 1215
760 DW=1 :REM DESTINATION WORLD COUNT
765 FOR CC = A2-2 TO A2+2
770 FOR RR = A3-2 TO A3+2
775 IF CC < 1 OR RR < 1 GOTO 785
780 IF CC <> A2 OR RR <> A3 THEN GOSUB 875 
785 NEXT
790 NEXT
795 ?:? SPC(25) "X: RETURN"
800 ?:?
805 A5 = 0
810 GET DE$
815    IF DE$="X" THEN A1 = 1 :GOTO 630
820    IF DE$="" GOTO 810
825    FP=ASC(DE$)
830    FP = FP - 64
835    IF FP < 1 OR FP > DW GOTO 810
840    ? "TARGET" FP "SELECTED."
845    A5 = DC(FP)
850    A6 = DR(FP)
855    INPUT "PRESS <RETURN>"; YN$
860    A1 = 1
865 GOTO 630
870 REM FETCH UWP DATA(CC,RR)
875    IF DW = 17 THEN RETURN
880    REM FIRST POINT TO THE RIGHT BANK
885    BA = INT((CC-1)/4)
890    POKE $9F61,11 + BA 
895    REM NOW CALCULATE THE ACTUAL COLUMN
900    CA = CC-1
905    CA = 4*(CA/4-INT(CA/4))
910    AD = $A000 + CA * 32 * 64 + (RR-1) * 32 * 1
915    IF PEEK(AD+10) = 32 THEN RETURN
920    DC(DW) = CC 
925    DR(DW) = RR
930    ? SPC(24) CHR$(DW+64) " "; :FOR X = 2 TO 27: ? CHR$(PEEK(AD+X)); :NEXT
935    IF PEEK(AD+28) = ASC(" ") THEN ? " \X99OK\X9B"
940    IF PEEK(AD+28) = ASC("A") THEN ? " \X9ECAUTION\X9B"
945    IF PEEK(AD+28) = ASC("R") THEN ? " \X96DANGER\X9B"
950    IF DW/4 = INT(DW/4) THEN ? SPC(24);
955    IF DW/4 = INT(DW/4) THEN ? "\X97....................................\X9B"
960    DW=DW+1
965 RETURN
970 ? CHR$(147) "\X11\X11"
975 BA$ = "  KAR"          
980 GOSUB 1215
985 ?:? SPC(25) "<YOUR CARGO AND VALUE>"
990 ?:? SPC(25) "<SYSTEM CARGO AND VALUE>"
995 ?:? SPC(25) "X: RETURN"
1000 GET SE$
1005    IF SE$="X" THEN A1 = 1
1010    IF SE$ < "1" OR SE$ > "X" GOTO 1000
1015 GOTO 630
1020 ? CHR$(147) "\X11\X11"
1025 BA$ = "  KHISDELIKUR"
1030 GOSUB 1215
1035 AL$ = "I" :GOSUB 445 :REM SHOW IMPERIAL SHIPS
1040 ?:? SPC(25) "<COMPONENT UPGRADES FOR SALE>"
1045 ?:? SPC(25) "<NEW COMPONENTS FOR SALE>"
1050 ?:? SPC(25) "X: RETURN"
1055 GET SE$
1060    IF SE$="X" THEN A1 = 1
1065    IF SE$ < "1" OR SE$ > "X" GOTO 1055
1070 GOTO 630
1075 ? CHR$(147) "\X11\X11"
1080 BA$ = "  KHIMEKKI"
1085 GOSUB 1215
1090 ?:? SPC(25) "<CHARACTER LIST>"
1095 ?:? SPC(25) "X: RETURN"
1100 GET SE$
1105    IF SE$="X" THEN A1 = 1
1110    IF SE$ < "1" OR SE$ > "X" GOTO 1100
1115 GOTO 630
1120 ? CHR$(147) "\X11\X11"
1125 BA$ = "  MEDISHE"
1130 GOSUB 1215
1135 ? SPC(25) "THE SCOUTS BUY YOUR SURVEY FOR CR100,000."
1140 GET A$ : IF A$="" GOTO 1140
1145 A1 = 1
1150 GOTO 630
1155 ? CHR$(147) "\X11\X11"
1160 BA$ = "  KIISHMU"
1165 GOSUB 1215
1170 ? SPC(25) "THE NAVY BUYS YOUR RECONNAISSANCE DATA FOR CR100,000."
1175 GET A$ : IF A$="" GOTO 1175
1180 A1 = 1
1185 GOTO 630
1190 HR$ = "\X1C"
1195 FOR X = 1 TO 80 :HR$ = HR$ + "\XC3" :NEXT
1200 HR$ = HR$ + "\X9B"
1205 RETURN
1210 REM BANNER(BA$)
1215 POKE $9F61,4 :REM BANNER CODE
1220 ? HR$;
1225 FOR X = 1 TO LEN(BA$)
1230    A = ASC(MID$(BA$,X,1))
1235    POKE $A000,A
1240    SYS $A001
1245 NEXT X
1250 ?:?:?:? HR$
1255 RETURN
1260 REM DISTANCE(C1,R1,C2,R2) -> D%
1265 A1 = R1 + INT(C1/2)
1270 A2 = R2 + INT(C2/2)
1275 D1% = ABS(A1-A2)
1280 D2% = ABS(C1-C2)
1285 D% = ABS( A1 - A2 - C1 + C2 )
1290 IF (D1% >= D2%) AND (D1% >= D%) THEN D% = D1%
1295 IF (D2% >= D1%) AND (D2% >= D%) THEN D% = D2%
1300 RETURN
1305 REM DICE(DM%) -> 2D6+DM% -> DM%
1310 DM% = DM% + 2 + INT(RND(1)*6) + INT(RND(1)*6)
1315 RETURN
1320 REM FLUX() -> FL%
1325 FL% = 6 * (RND(1)-RND(1))
1330 RETURN
1335 REM EHEX2DEC(HX$) -> N%
1340 N%=0
1345 IF HX$ <= "9" THEN N%=ASC(HX$)-48 :RETURN
1350 IF HX$ >= "P" THEN N%=ASC(HX$)-57 :RETURN
1355 IF HX$ >= "J" THEN N%=ASC(HX$)-56 :RETURN
1360 IF HX$ >= "A" THEN N%=ASC(HX$)-55 :RETURN
1365 RETURN
1370 REM   DECOMPRESS BYTE PAIRS OF UWP-TEXT
1375 REM   PASSED IN AS B0%(), COUNT IN B0%(0)
1380 REM   OUTPUT TO O$
1385 REM   ASSUME B0%() IS INITIALIZED
1390 AL$(0) = " ABCDEFGHIJKLMNOPQRSTUVWXYZ?:'-"
1395 AL$(1) = " 0123456789ABCDEFGHILMNORSTUWY-"
1400 O$ = "": V=0: FOR BB = 1 TO B0%(0) STEP 2
1405 B0% = B0%(BB)
1410 B1% = B0%(BB+1)
1415 REM ? BB, B0%, B1%
1420 B2% = B0% AND 31
1425 B3% = (B0%/32) + (B1% AND 3)*8
1430 B4% = (B1% AND 127) / 4
1435 REM TRANSLATE
1440 B% = B2% :GOSUB 1465 :REM DECODE
1445 B% = B3% :GOSUB 1465 :REM DECODE
1450 B% = B4% :GOSUB 1465 :REM DECODE
1455 NEXT
1460 RETURN
1465 IF B% = 31 THEN V = 1 - V :RETURN
1470 REM ? "INDEX: " B%+1 " LETTER: " MID$(AL$(V),B%+1,1)
1475 O$ = O$ + MID$(AL$(V),B%+1,1)
1480 RETURN
