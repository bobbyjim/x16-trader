5 COLOR 15,0 :?CHR$(147)	:REM LT GREY ON BLACK
10 DIM BU$(30) :REM LINE BUFFER
15 DIM DX(16),DY(16),DC(16),DR(16) :REM DESTINATION VECTORS
20 LOAD "ALLSHIPS.BIN"      ,8, 3,$A000 :REM BANK 3
25 LOAD "BANNER.BIN"        ,8, 4,$A000 :REM BANK 4
30 LOAD "SPINWARD.SPACE.BIN",8,11,$A000 :REM BANK 11 AND UP
35 GOSUB 2720
40 GOSUB 1265
45 GOSUB 1990
50 C6 = 100 :REM KCR
55 A1 = 19         :REM COL
60 A2 = 10         :REM ROW
65 A3 = 2        :REM RANGE
70 A5 = 0    :REM DEST.AROW
75 A4 = 0    :REM DEST.ACOL
80    GOSUB 2135
85    GOSUB 1330
90    GOTO 80
95 REM
100 REM ------------------- INITIALIZE ENEMY ------------------
105 REM
110 REM  FOR NOW, LET'S JUST USE A SIMPLE DAMAGE LEVEL
115 B9 = INT(RND(1)*5)+INT(RND(1)*4)+2 :REM 200 TO 900 TONS
120 C0 = 0
125 RETURN
130 REM ---------------------- COMBAT -----------------------
135 REM
140 REM  THE MAJOR OUTCOME OF COMBAT IS SURVIVAL OR DESTRUCTION.
145 REM
150 REM  DESTRUCTION IS THE LOSS OF THE SHIP, AND PERHAPS THE
155 REM  LIVES OF THE CREW.
160 REM  
165 REM  SURVIVAL IS ENUMERATED AS:
170 REM
175 REM   2-9 - YOU'VE WON, AND CAPTURED A 200-900 TON CORSAIR
180 REM   1   - YOU'VE RUN VIA A SUCCESSFUL EMERGENCY JUMP
185 REM   0   - THE ENEMY IS REPELLED AND RUNS AWAY
190 REM  -1   - THE ENEMY PLUNDERS YOUR CARGO AND VEHICLES
195 REM
200 B5 = 1
205 B6 = 30
210 B7 = 30
215 B8 = 30
220 C5 = 0
225 GOSUB 115
230 ? HR$ "  SENSOP'S REPORT" :? HR$;
235 ? "  KEPTIN, VE ARE FACING A" B9 * 100 "TON CORSAIR\X11"
240 GOSUB 305
245 IF C5 = -1 THEN GOSUB 260
250 IF C5 > 1  THEN GOSUB 265
255 RETURN
260 RETURN
265 ? HG$ "  INSYSTEM SALVAGE REPORT" :?HG$;
270 S = C5 + INT(RND(1)*C5)
275 ?
280 ? "   THANK YOU FOR DEFEATING THIS SCUM. AS A REWARD, YOUR SHARE"
285 ? "   OF THE CORSAIR'S SALVAGE VALUE COMES TO MCR" C5 "."
290 C6 = C6 + S * 1000
295 RETURN
300 REM --------------------- COMBAT LOOP ------------------
305    ? HR$ "  HELM REPORT, ROUND"; B5: ? HR$;
310    ? "  CAPTAIN, SHALL WE:\X11"
315    ? "    1 - ATTACK!"
320    ? "    2 - DEPLOY REPAIR DETAIL!"   
325    ? "    3 - DEPLOY MEDICAL DETAIL!"
330    ? "    4 - MAKE AN EMERGENCY JUMP!"
335    ? "    5 - SIGNAL SURRENDER!\X11"
340    INPUT "  YOUR COMMAND"; CM$ :CM = VAL(CM$)
345    IF CM < 1 OR CM > 5 GOTO 340
350    ? CHR$(147);
355    B6 = 30 :B7 = 30 :B8 = 30
360    IF CM = 1 THEN B6 = 60
365    IF CM = 2 THEN B7 = 60
370    IF CM = 3 THEN B8 = 60
375    IF CM = 4 THEN GOSUB 1015 :IF C5=1 THEN RETURN
380    IF CM = 5 GOTO 1135
385    GOSUB 435  :INPUT "   PRESS <RETURN>"; YN$
390    GOSUB 755  :INPUT "   PRESS <RETURN>"; YN$
395    GOSUB 875 :INPUT "   PRESS <RETURN>"; YN$
400    GOSUB 670 :IF C5=1 THEN RETURN
405    GOSUB 605 :IF WW=1 THEN RETURN
410    B5 = B5 + 1
415 GOTO 305
420 REM
425 REM -------------------- BATTLE ACTION -----------------
430 REM
435 ? HR$ "  BATTLE REPORT" :?HR$;
440 FOR X = 6 TO C4
445    Q=0
450    FOR P = 1 TO C3(X)
455       Q = Q + INT(RND(1)*6)
460    NEXT
465    IF Q > 8 GOTO 500
470    D = INT(RND(1)*B6)
475    ? "   TURRET" X-5;
480    IF D < 11 THEN ? "NICKS THE ENEMY SHIP."
485    IF D > 10 AND D < 21 THEN ? "HITS THE ENEMY SHIP!"
490    IF D > 20 THEN ? "WALLOPS THE ENEMY SHIP!!"
495    C0 = C0 + D
500 NEXT
505 TD = C0 / (B9 * 100)
510 IF TD > 0.25 THEN ?:? "   THE CORSAIR'S HULL IS SHOWING SIGNS OF DAMAGE."
515 IF TD > 0.5  THEN ?:? "   FIRE IS DETECTED IN THE CORSAIR."
520 IF TD > 0.75 THEN ?:? "   THE CORSAIR IS SHOWING SERIOUS DAMAGE."
525 ?
530 FOR X = 1 TO B9
535    IF INT(RND(1)*6)+INT(RND(1)*6)+2 > 6 GOTO 575
540    T = INT(RND(1)*C4)
545    ? "   OUR " C1$(T) " IS HIT."
550    C3(T) = C3(T)+1
555    IF INT(RND(1)*6)+INT(RND(1)*6)+2 > 6 GOTO 575
560    C = INT(RND(1)*B3)
565    ? "   \X9A" B1$(C) "\X9B IS HIT."
570    B2(C) = B2(C) + 1
575 NEXT 
580 ?
585 RETURN
590 REM
595 REM --------------- CHECK ENEMY STATUS --------------
600 REM
605 WW = 0
610 IF C0 < B9 * 100 THEN RETURN
615 WW = 1
620 IF C0 - B9 * 100 > 100 GOTO 635
625 ? HR$ "  SENSOP'S REPORT" :? HR$;
630 ? "   ZE ENEMY RUNS!  VE HAVE VON!!" :RETURN
635 ? HR$ "  HELM REPORT" :?HR$
640 ? "   THE ENEMY SURRENDERS!"
645 ? "   WE HAVE CLAIMED SALVAGE RIGHTS TO THEIR" B9*100 "TON CORSAIR!"
650 RETURN
655 REM
660 REM --------------- CHECK POWERPLANT STATUS --------------
665 REM
670 IF C3(4) = C2(4)+1 GOTO 720
675 IF C3(4) <= C2(4)+1 THEN RETURN
680 IF B2(2) > 1 GOTO 705
685 ? HR$ "  ENGINEER'S REPORT" :?HR$;
690 ? "\X11\X9E  ACH CAPTAIN, THE POWERRR PLANT'S A COMPLETE WRRRECK!"
695 ? "  WE'RE DEAD IN TH' WATERRR!\X9B\X11"
700 GOTO 1150
705 ? HR$ "  HELM REPORT" :?HR$;
710 ? "\X11\X93  CAPTAIN, THE ENGINES AREN'T RESPONDING. THE SHIP APPEARS TO BE DEAD.\X9B\X11"
715 GOTO 1150
720 ? HR$ "  *** ENGINEER'S WARNING ***" :?HR$;
725 ? "\X11\X9E  ACH CAPTAIN, THE POWERRR PLANT'S A MESS."
730 ? "  ONE MORE HIT LIKE THAT AN' WE'RRRE FINISHED!\X9B\X11"
735 RETURN
740 REM
745 REM -------------------- REPAIR ACTION -----------------
750 REM
755 ? HR$ "  DAMAGE CONTROL REPORT" :?HR$;
760 IF B2(2) > 1 THEN ? "  THE ENGINEER IS HURT; NO REPORT AVAILABLE.\X11" :RETURN
765 ? "", "COMPONENT",, "STATUS"
770 ? HW$
775 FOR X = 0 TO C4
780    IF C3(X) > 0 AND INT(RND(1)*B7) > 19 THEN C3(X) = C3(X) - 1
785    IF C3(X) > 0 AND INT(RND(1)*B7) > 29 THEN C3(X) = C3(X) - 1
790    IF C3(X) > 0 AND INT(RND(1)*B7) > 39 THEN C3(X) = C3(X) - 1
795    IF X = 5 THEN ? DL$;
800    ? "   ",
805    ? LEFT$(C1$(X)+"             ",16),
810    A = C2(X)
815    B = C3(X)
820    IF B>0 AND B>A AND X<6 THEN ? "   " + CHR$(28);
825    IF B>0 AND B>A AND X<6 THEN ? "OUT OF ACTION" + CHR$(155)
830    IF B>0 AND (B<=A OR X>5) THEN ? "   " + CHR$(158);
835    IF B>0 AND (B<=A OR X>5) THEN ? "DAMAGED" + CHR$(155)
840    IF B=0 THEN ? "   " + CHR$(153) + "OK" + CHR$(155)
845 NEXT
850 ?
855 RETURN
860 REM
865 REM -------------------- MEDICAL ACTION -----------------
870 REM
875 ? HR$ "  MEDICAL REPORT" :?HR$;
880 IF B2(3) > 1 THEN ? "  THE MEDIC IS HURT; NO REPORT AVAILABLE.\X11" :RETURN
885 ? "", "CREWMEMBER", "ROLE      ", "WOUNDS"
890 ? HW$
895 FOR X = 0 TO B3
900    IF B2(X) > 0 AND INT(RND(1)*B8) > 19 THEN B2(X)=B2(X)-1
905    IF B2(X) > 0 AND INT(RND(1)*B8) > 29 THEN B2(X)=B2(X)-1
910    IF B2(X) > 0 AND INT(RND(1)*B8) > 39 THEN B2(X)=B2(X)-1
915    IF X = 4 THEN ? DL$;
920    ? "   ",
925    ? LEFT$(B1$(X)+"            ",16),
930    R$ = "          "
935    IF X = 0 THEN R$ = "PILOT     "
940    IF X = 1 THEN R$ = "ASTROGATOR"
945    IF X = 2 THEN R$ = "ENGINEER  "
950    IF X = 3 THEN R$ = "MEDIC     "
955    IF X = 4 THEN R$ = "STEWARD   "
960    ? R$,
965    W$ = CHR$(28) + "OUT OF ACTION" + CHR$(155)
970    IF B2(X) = 0 THEN W$ = CHR$(153) + "OK" + CHR$(155)
975    IF B2(X) = 1 THEN W$ = CHR$(158) + "WOUNDED" + CHR$(155)
980    ? W$
985 NEXT
990 ?
995 RETURN
1000 REM
1005 REM ------------------ EMERGENCY JUMP --------------------
1010 REM
1015 IF B2(1) > 0 GOTO 1055
1020 IF B2(2) > 0 GOTO 1070
1025 IF C3(3) >= C2(3) GOTO 1100
1030 IF INT(RND(1)*6)+INT(RND(1)*6)+2 > 6 GOTO 1085
1035 ? "   AYE, YOU'VE GOT IT, CAPTAIN!"
1040 FOR T = 1 TO 1000 : TT = SQR(12345) :NEXT
1045 ? "   CAPTAIN, WE'VE SUCCESSFULLY JUMPED TO SAFETY!"
1050 C5=1 :RETURN
1055 ? HR$ "   HELM REPORT" :? HR$;
1060 ? "   OUR ASTROGATOR IS WOUNDED, AND CANNOT CALCULATE A JUMP.\X11"
1065 C5=0 :RETURN
1070 ? HR$ "   HELM REPORT" :? HR$;
1075 ? "   OUR ENGINEER IS WOUNDED, AND CANNOT OPERATE THE JUMP DRIVE.\X11"
1080 C5=0 :RETURN
1085 ? HR$ "   ENGINEER'S REPORT" :? HR$;
1090 ? "   ACH, WE KENNA JUMP YET CAPTAIN, GIMME 20 MORRRE MINUTES!\X11"
1095 C5=0 :RETURN
1100 ? HR$ "   ENGINEER'S REPORT" :? HR$;
1105 ? "   ACH, THE JUMP DRIVES ARRRE DOON, CAPTAIN!"
1110 ? "   WE NE KENNA JUMP UNTIL THEY'RRRE REPAIRRRED!\X11"
1115 C5=0 :RETURN
1120 REM
1125 REM --------------------- SURRENDER ----------------------
1130 REM
1135 ? HR$ "  HELM REPORT": ?HR$;
1140 ? "  SURRENDER ACKNOWLEDGED. CREW IS ARMING THEMSELVES.\X11"
1145 FOR T = 1 TO 1000: TT=SQR(12345) :NEXT
1150 ? HR$ "  HELM REPORT": ?HR$;
1155 ? "  CAPTAIN, WE HAVE BEEN BOARDED."
1160 FOR T = 1 TO 1000: TT=SQR(12345) :NEXT
1165 ? "  FIGHTING REPORTED IN ALL AREAS."
1170 FOR T = 1 TO 1000: TT=SQR(12345) :NEXT
1175 C5 = -1
1180 RR = INT(RND(1)*6)+INT(RND(1)*6)+2
1185 IF RR < 6 THEN ? "  WE HAVE REPELLED THE PIRATES!!" :C5 = 1 :RETURN
1190 IF RR < 8 THEN ? "  OUR CARGO AND VEHICLES HAVE BEEN TAKEN." :RETURN
1195 IF RR < 10 THEN ? "  WE HAVE BEEN STRANDED ON A MARGINAL WORLD." :END
1200 ? "  THE CREW HAS BEEN SOLD INTO SLAVERY." :END
1205 REM 
1210 REM ---------------- INITIALIZE CREW ----------------
1215 REM
1220 REM  CREW ROLES ARE INDIVIDUALIZED AS FOLLOWS:
1225 REM
1230 REM    0 - PILOT
1235 REM    1 - ASTROGATOR
1240 REM    2 - ENGINEER
1245 REM    3 - MEDIC
1250 REM    4 - STEWARD/FREIGHTMASTER
1255 REM    5 AND UP - GUNNERS
1260 REM
1265 DIM B1$(24), B2(24), B4(24)
1270 B3 = -1
1275    B3 = B3+1
1280    READ B1$(B3)
1285    B2(B3) = 0
1290    B4 = B3
1295    IF B1$(B3) <> "-1" GOTO 1275
1300 B3 = B3 - 1
1305 DATA JAMISON, AKRIND, KODRAY, ARRLANROUGHL, REYNALDI, SHARIK, -1
1310 RETURN
1315 REM
1320 REM -------------------------- SYSTEM -----------------------
1325 REM
1330    COLOR 15,0 :REM WHITE ON BLACK
1335    A0 = 1
1340 REM
1345 REM  THERE IS ONLY ONE EXIT OUT OF THE SYSTEM, AND THAT IS "STARPORT"
1350 REM
1355    ON A0 GOTO 1365, 1440, 1445, 1495, 1545
1360    GOTO 1355
1365 ? CHR$(147) "\X11\X11"
1370 BA$ = "      INSYSTEM"
1375 GOSUB 2750
1380 ?:? SPC(25) "YOU ARE IN ORBIT AROUND ";
1385 GOSUB 1620
1390 ?:?
1395 ?:? SPC(25) "1 - LAND AT STARPORT"
1400 ?:? SPC(25) "2 - WILDERNESS REFUELING"
1405 ?:? SPC(25) "3 - SYSTEM SURVEY"
1410 ?:? SPC(25) "4 - JUMP"
1415 ?:?
1420 GET DE$
1425 IF DE$ < "1" OR DE$ > "4" GOTO 1420
1430 A0 = VAL(DE$)+1
1435 GOTO 1355
1440 RETURN
1445 ? CHR$(147) "\X11\X11"
1450 BA$ = "      REFUEL"
1455 GOSUB 2750
1460 ?:? SPC(25) "<REFUEL OPTIONS>"
1465 ?:? SPC(25) "X: RETURN"
1470 ?:?
1475 GET DE$
1480    IF DE$="X" THEN A0 = 1
1485    IF DE$ < "1" OR DE$ > "X" GOTO 1475
1490 GOTO 1355
1495 ? CHR$(147) "\X11\X11"
1500 BA$ = "      SURVEY"
1505 GOSUB 2750
1510 ?:? SPC(25) "<SURVEY OPTIONS>"
1515 ?:? SPC(25) "X: RETURN"
1520 ?:?
1525 GET DE$
1530    IF DE$="X" THEN A0 = 1
1535    IF DE$ < "1" OR DE$ > "X" GOTO 1525
1540 GOTO 1355
1545 ? CHR$(147) "\X11\X11"
1550 BA$ = "      JUMP"
1555 GOSUB 2750
1560 A1 = A4 
1565 A2 = A5
1570 A4 = 0 :REM UNSET FLIGHT PLAN
1575 A5 = 0
1580 ?:? SPC(25) "WELCOME TO ";
1585 GOSUB 1620
1590 ?:?
1595 IF P = 1 THEN P = 0 :GOSUB 200
1600 INPUT "PRESS <RETURN>"; YN$
1605 A0 = 1
1610 GOTO 1355
1615 REM MAP.WHERE AM I(COL,ROW)
1620    REM FIRST POINT TO THE RIGHT BANK
1625    BA = INT((A1-1)/4)
1630    POKE $9F61,11+BA
1635    REM NOW CALCULATE THE ACTUAL COLUMN
1640    CA = A1 - 1
1645    CA = 4*(CA/4-INT(CA/4))
1650    AD = $A000 + CA * 32 * 64 + (A2-1) * 32 * 1
1655    FOR X = 10 TO 24: ? CHR$(PEEK(AD+X)); :NEXT
1660    A6 = PEEK(AD+7)
1665    A7 = PEEK(AD+8)
1670    A8 = PEEK(AD+26)
1675    A9 = PEEK(AD+27)
1680    B0 = PEEK(AD+28)
1685    REM CHECK FOR PIRATES
1690    P = 3
1695    IF A6 = 65 THEN P = 1
1700    IF A6 = 66 THEN P = 2
1705    IF A6 = 67 THEN P = 3
1710    IF A6 = 68 THEN P = 4
1715    IF A6 = 69 THEN P = 5
1720    IF A6 = 88 THEN P = 6
1725    IF B0 = 65 THEN P = P + 2
1730    IF B0 = 82 THEN P = P + 4
1735    IF INT(RND(1)*6)+INT(RND(1)*6)+2 < P THEN P = 1 :RETURN
1740    P = 0 :RETURN
1745 REM  GET REGIONAL UWPS(COL,ROW,RNG)
1750 POKE $9F61,11 :REM MAP
1755 DW = 0
1760 FOR CC = A1 - A3 TO A1 + A3
1765 FOR RR = A2 - A3 TO A2 + A3
1770 NEXT RR
1775 NEXT CC
1780    ? CHR$(147) "SHIP LIST\X11"
1785    POKE $9F61,3  :REM SHIPS
1790    AD = $A000
1795    CT = 0
1800    IF PEEK(AD) = 0 THEN 1915
1805    O% = PEEK(AD+28) :REM OWNER
1810    L = PEEK(AD+3)   :REM COMPONENT LENGTH
1815    T = PEEK(AD+24)  :REM SIZE CODE
1820    M1 = PEEK(AD+22) :REM MISSION CODE 1
1825    IF A9 <> O% GOTO 1905
1830    IF M1 = ASC("E") GOTO 1905 :REM NO ESCORTS
1835    IF M1 = ASC("C") GOTO 1905 :REM NO CRUISERS
1840    ? "     ";
1845    ? CHR$(M1);          :REM MISSION CODE 1
1850    ? CHR$(PEEK(AD+23)); :REM MISSION CODE 2
1855    ? " "; 
1860    FOR X = 4 TO 21 : ? CHR$(PEEK(AD+X)); :NEXT
1865    ? T*100;
1870    IF T<10  THEN ? " ";
1875    ? "TONS";
1880    MC = PEEK(AD+27)
1885    ? "  MCR " MC*T
1890    CT=CT+1
1895    IF CT/4 <> INT(CT/4) GOTO 1905
1900    ? "     -----------------------------------------"
1905    AD = AD + 4 + L :REM NEXT RECORD
1910    GOTO 1800
1915    IF CT = 0 THEN ? "     NO SHIPS AVAILABLE."
1920    RETURN
1925 REM
1930 REM ----------------- INITIALIZE SHIP ------------------
1935 REM
1940 REM   "SUBTYPE" IS DRIVE RATING, WEAPON SUBTYPE, OR FUEL AMOUNT.
1945 REM
1950 REM   COMPONENT 0 IS "THE HULL".
1955 REM   COMPONENT 1 IS STREAMLINING, GENERALLY.
1960 REM   COMPONENT 2 IS THE MANEUVER DRIVE.
1965 REM   COMPONENT 3 IS THE JUMP DRIVE.
1970 REM   COMPONENT 4 IS THE POWER PLANT.
1975 REM   COMPONENT 5 IS FUEL TANKAGE.
1980 REM   COMPONENT 6 AND UP ARE DEFENSES.
1985 REM
1990 DIM C1$(40), C2(40), C3(40)
1995 C4=-1
2000    C4=C4+1
2005    READ C1$(C4)
2010    READ C2(C4)
2015    C3(C4) = 0
2020    IF C1$(C4) <> "-1" GOTO 2000
2025 C4 = C4 - 1
2030 DATA HULL,         	0 
2035 DATA STREAMLINING, 	0 
2040 DATA MANEUVER DRIVE,    1 
2045 DATA JUMP DRIVE,        1 
2050 DATA POWER PLANT,       1 
2055 DATA FUEL TANKAGE,      10
2060 DATA TURRET 1,      	2
2065 DATA TURRET 2,     	2
2070 REM DATA TURRET 1L,   21,   TURRET 2L,  22,  TURRET 3L,  23
2075 REM DATA TURRET 1M,   31,   TURRET 2M,  32,  TURRET 3M,  33
2080 REM DATA TURRET 1S,   41,   TURRET 2S,  42,  TURRET 3S,  43
2085 REM
2090 REM DATA BARBETTE L,  53,   BAY L,      54
2095 REM DATA BARBETTE M,  63,   BAY M,      64
2100 REM DATA BARBETTE S,  73,   BAY S,      74
2105 REM DATA BARBETTE A,  83,   BAY A,      84
2110 DATA -1, -1
2115 RETURN
2120 REM
2125 REM -------------------------- STARPORT -----------------------
2130 REM
2135    COLOR 15,0 :REM WHITE ON BLACK
2140    A0 = 1
2145 REM 
2150 REM THERE IS ONLY ONE EXIT OUT OF THE STARPORT, AND THAT IS "EXIT STARPORT"
2155 REM
2160    ON A0 GOTO 2175, 2275, 2500, 2550, 2605, 2650, 2685, 2170
2165    GOTO 2160
2170    RETURN
2175 ? CHR$(147) "\X11\X11"
2180 BA$ = "  KIGUKI"
2185 GOSUB 2750
2190 ?:? SPC(25) "WELCOME TO ";
2195 GOSUB 1620
2200 ?:?
2205 ?:? SPC(25) "1 - FILE FLIGHT PLAN";
2210 IF A4 > 0 THEN ? " (FILED)"
2215 IF A4 = 0 THEN ?
2220 ?:? SPC(25) "2 - COMMODITY EXCHANGE"
2225 ?:? SPC(25) "3 - SHIPYARD"
2230 ?:? SPC(25) "4 - HIRING HALL"
2235 ?:? SPC(25) "5 - SCOUT BASE"
2240 ?:? SPC(25) "6 - NAVAL BASE"
2245 ?:? SPC(25) "7 - EXIT STARPORT"
2250 ?:?
2255 GET DE$
2260 IF DE$ < "1" OR DE$ > "7" GOTO 2255
2265 A0 = VAL(DE$)+1
2270 GOTO 2160
2275 ? CHR$(147) "\X11\X11"
2280 BA$ = "  GIMGU"
2285 GOSUB 2750
2290 DW=1 :REM DESTINATION WORLD COUNT
2295 FOR CC = A1-2 TO A1+2
2300 FOR RR = A2-2 TO A2+2
2305 IF CC < 1 OR RR < 1 GOTO 2315
2310 IF CC <> A1 OR RR <> A2 THEN GOSUB 2405 
2315 NEXT
2320 NEXT
2325 ?:? SPC(25) "X: RETURN"
2330 ?:?
2335 A4 = 0
2340 GET DE$
2345    IF DE$="X" THEN A0 = 1 :GOTO 2160
2350    IF DE$="" GOTO 2340
2355    FP=ASC(DE$)
2360    FP = FP - 64
2365    IF FP < 1 OR FP > DW GOTO 2340
2370    ? "TARGET" FP "SELECTED."
2375    A4 = DC(FP)
2380    A5 = DR(FP)
2385    INPUT "PRESS <RETURN>"; YN$
2390    A0 = 1
2395 GOTO 2160
2400 REM FETCH UWP DATA(CC,RR)
2405    IF DW = 17 THEN RETURN
2410    REM FIRST POINT TO THE RIGHT BANK
2415    BA = INT((CC-1)/4)
2420    POKE $9F61,11 + BA 
2425    REM NOW CALCULATE THE ACTUAL COLUMN
2430    CA = CC-1
2435    CA = 4*(CA/4-INT(CA/4))
2440    AD = $A000 + CA * 32 * 64 + (RR-1) * 32 * 1
2445    IF PEEK(AD+10) = 32 THEN RETURN
2450    DC(DW) = CC 
2455    DR(DW) = RR
2460    ? SPC(24) CHR$(DW+64) " "; :FOR X = 2 TO 27: ? CHR$(PEEK(AD+X)); :NEXT
2465    IF PEEK(AD+28) = ASC(" ") THEN ? " \X99OK\X9B"
2470    IF PEEK(AD+28) = ASC("A") THEN ? " \X9ECAUTION\X9B"
2475    IF PEEK(AD+28) = ASC("R") THEN ? " \X96DANGER\X9B"
2480    IF DW/4 = INT(DW/4) THEN ? SPC(24);
2485    IF DW/4 = INT(DW/4) THEN ? "\X97....................................\X9B"
2490    DW=DW+1
2495 RETURN
2500 ? CHR$(147) "\X11\X11"
2505 BA$ = "  KAR"          
2510 GOSUB 2750
2515 ?:? SPC(25) "<YOUR CARGO AND VALUE>"
2520 ?:? SPC(25) "<SYSTEM CARGO AND VALUE>"
2525 ?:? SPC(25) "X: RETURN"
2530 GET SE$
2535    IF SE$="X" THEN A0 = 1
2540    IF SE$ < "1" OR SE$ > "X" GOTO 2530
2545 GOTO 2160
2550 ? CHR$(147) "\X11\X11"
2555 BA$ = "  KHISDELIKUR"
2560 GOSUB 2750
2565 AL$ = "I" :GOSUB 1780 :REM SHOW IMPERIAL SHIPS
2570 ?:? SPC(25) "<COMPONENT UPGRADES FOR SALE>"
2575 ?:? SPC(25) "<NEW COMPONENTS FOR SALE>"
2580 ?:? SPC(25) "X: RETURN"
2585 GET SE$
2590    IF SE$="X" THEN A0 = 1
2595    IF SE$ < "1" OR SE$ > "X" GOTO 2585
2600 GOTO 2160
2605 ? CHR$(147) "\X11\X11"
2610 BA$ = "  KHIMEKKI"
2615 GOSUB 2750
2620 ?:? SPC(25) "<CHARACTER LIST>"
2625 ?:? SPC(25) "X: RETURN"
2630 GET SE$
2635    IF SE$="X" THEN A0 = 1
2640    IF SE$ < "1" OR SE$ > "X" GOTO 2630
2645 GOTO 2160
2650 ? CHR$(147) "\X11\X11"
2655 BA$ = "  MEDISHE"
2660 GOSUB 2750
2665 ? SPC(25) "THE SCOUTS BUY YOUR SURVEY FOR CR100,000."
2670 GET A$ : IF A$="" GOTO 2670
2675 A0 = 1
2680 GOTO 2160
2685 ? CHR$(147) "\X11\X11"
2690 BA$ = "  KIISHMU"
2695 GOSUB 2750
2700 ? SPC(25) "THE NAVY BUYS YOUR RECONNAISSANCE DATA FOR CR100,000."
2705 GET A$ : IF A$="" GOTO 2705
2710 A0 = 1
2715 GOTO 2160
2720 HR$ = ""
2725 FOR X = 1 TO 80 :HR$ = HR$ + "\XC3" :NEXT
2730 HG$ = "\X99" + HR$ + "\X9B"
2735 HR$ = "\X1C" + HR$ + "\X9B"
2740 RETURN
2745 REM BANNER(BA$)
2750 POKE $9F61,4 :REM BANNER CODE
2755 ? HR$;
2760 FOR X = 1 TO LEN(BA$)
2765    A = ASC(MID$(BA$,X,1))
2770    POKE $A000,A
2775    SYS $A001
2780 NEXT X
2785 ?:?:?:? HR$
2790 RETURN
2795 REM DISTANCE(C1,R1,C2,R2) -> D%
2800 A1 = R1 + INT(C1/2)
2805 A2 = R2 + INT(C2/2)
2810 D1% = ABS(A1-A2)
2815 D2% = ABS(C1-C2)
2820 D% = ABS( A1 - A2 - C1 + C2 )
2825 IF (D1% >= D2%) AND (D1% >= D%) THEN D% = D1%
2830 IF (D2% >= D1%) AND (D2% >= D%) THEN D% = D2%
2835 RETURN
2840 REM DICE(DM%) -> 2D6+DM% -> DM%
2845 DM% = DM% + 2 + INT(RND(1)*6) + INT(RND(1)*6)
2850 RETURN
2855 REM FLUX() -> FL%
2860 FL% = 6 * (RND(1)-RND(1))
2865 RETURN
2870 REM EHEX2DEC(HX$) -> N%
2875 N%=0
2880 IF HX$ <= "9" THEN N%=ASC(HX$)-48 :RETURN
2885 IF HX$ >= "P" THEN N%=ASC(HX$)-57 :RETURN
2890 IF HX$ >= "J" THEN N%=ASC(HX$)-56 :RETURN
2895 IF HX$ >= "A" THEN N%=ASC(HX$)-55 :RETURN
2900 RETURN
2905 REM   DECOMPRESS BYTE PAIRS OF UWP-TEXT
2910 REM   PASSED IN AS B0%(), COUNT IN B0%(0)
2915 REM   OUTPUT TO O$
2920 REM   ASSUME B0%() IS INITIALIZED
2925 AL$(0) = " ABCDEFGHIJKLMNOPQRSTUVWXYZ?:'-"
2930 AL$(1) = " 0123456789ABCDEFGHILMNORSTUWY-"
2935 O$ = "": V=0: FOR BB = 1 TO B0%(0) STEP 2
2940 B0% = B0%(BB)
2945 B1% = B0%(BB+1)
2950 REM ? BB, B0%, B1%
2955 B2% = B0% AND 31
2960 B3% = (B0%/32) + (B1% AND 3)*8
2965 B4% = (B1% AND 127) / 4
2970 REM TRANSLATE
2975 B% = B2% :GOSUB 3000 :REM DECODE
2980 B% = B3% :GOSUB 3000 :REM DECODE
2985 B% = B4% :GOSUB 3000 :REM DECODE
2990 NEXT
2995 RETURN
3000 IF B% = 31 THEN V = 1 - V :RETURN
3005 REM ? "INDEX: " B%+1 " LETTER: " MID$(AL$(V),B%+1,1)
3010 O$ = O$ + MID$(AL$(V),B%+1,1)
3015 RETURN
