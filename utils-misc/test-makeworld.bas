10 INPUT "WORLD SEED"; SD$ 
20 SD = ABS(VAL(SD$)) :IF SD = 0 THEN SD = -TI
30 SD = RND(-SD)
40 ? CHR$(147);
50 TT = TI :TI$="000000"
60 R=30 :C=78 :DIM MA(C,R) :DIM CC(20)
70 FOR X = 0 TO 20 :READ CC(X) :NEXT
80 DATA 6,3,3,3,7,7,7,5,5,5,5,5,13,13,15,15,15,15,14,14,15,15
90 SS$(0) = "INITIATING JUMP PROGRAM"
100 SS$(1) = "JUMP DRIVES CHARGING"
110 SS$(2) = "ENTERING JUMPSPACE"
120 SS$(3) = "DAY 1"
130 SS$(4) = "DAY 2"
140 SS$(5) = "DAY 3"
150 SS$(6) = "DAY 4"
160 SS$(7) = "DAY 5"
170 SS$(8) = "DAY 6"
180 SS$(9) = "DAY 7: EXIT PRECIPITATION"
190 REM SS$(9) = "SCANNING LOCAL SYSTEM"
200 SS$(10) = "" : REM SYNCHRONIZING WITH LOCAL SYSTEM"
210 MU = 2 
220 FOR I = 0 TO 5 * MU 
230    X = INT(RND(1)*C-10)+5
240    Y = INT(RND(1)*R-10)+5
250    FOR J = 1 TO INT(RND(1)*R*C/(2*MU))
260       X=X+2*RND(1)-1: Y=Y+2*RND(1)-1
270       IF X<0 OR Y<0 OR X>C OR Y>R GOTO 290
280       MA(X,Y) = MA(X,Y)+1
290    NEXT J
300    IF SS$(I)<>"" THEN ? TI$ " : " SS$(I) "\X11"
310 NEXT I
320 GT$ = " S+" + STR$(VAL(TI$)) + " ":? GT$;
330 FOR X = LEN(GT$) TO C :? CHR$(195); :NEXT: ?
340 FOR J = 0 TO R-1
350    ? " ";
360    ME$ = " " :IF J = INT(R/2) THEN ME$ = "." :REM MERIDIAN LINE
370    FOR I = 0 TO C-1
380       IF MA(I,J) = 0 THEN ? ME$; :GOTO 420
390       CO = MA(I,J) :IF CO>20 THEN CO=20
400       COLOR CC(CO),6
410       PRINT "X"; 
420    NEXT I :?
430 NEXT J
440 TT$ = " S+" + STR$(VAL(TI$)) + " ":? TT$;
450 FOR X = LEN(TT$) TO C :? CHR$(195); :NEXT
460 COLOR 1,6
