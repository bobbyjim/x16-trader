5 COLOR 15,0 :? CHR$(147)
10 ? "TRAVELLER SPACE COMBAT V4\X11"
15 HW$ = ""
20 FOR X=1 TO 80 : HW$ = HW$ + CHR$(195) :NEXT X
25 HR$ = CHR$(28) + HW$ + CHR$(155)
30 DL$ = ""
35 FOR X=1 TO 80 : DL$ = DL$ + "." :NEXT X
40 ? HR$
45 ? "   BATTLE A CORSAIR AND ITS CREW OF REMORSELESS VARGR PIRATES!"
50 ?
55 ? "   TO WIN, YOU MUST PROTECT YOUR POWER PLANT"
60 ? "   AND WEAR DOWN THE ENEMY."
65 ?
70 ? "   GOOD LUCK!\X11"
75 ? HR$
80 INPUT "PRESS <RETURN> TO BEGIN"; YN$ :X = RND(-TI)
85 GOSUB 845
90 GOSUB 955
95 GOSUB 1105
100 A0   = 1
105 A1  = 30
110 A2  = 30
115 A3 = 30
120 ? CHR$(147);
125 ? HR$ "  SENSOP'S REPORT" :? HR$;
130 ? "  KEPTIN, VE ARE FACING A SIZE" A4 "CORSAIR\X11"
135 REM --------------------- MAIN LOOP ------------------
140    ? HR$ "  HELM REPORT, ROUND"; A0: ? HR$;
145    ? "  CAPTAIN, SHALL WE:\X11"
150    ? "    1 - ATTACK!"
155    ? "    2 - DEPLOY REPAIR DETAIL!"   
160    ? "    3 - DEPLOY MEDICAL DETAIL!"
165    ? "    4 - MAKE AN EMERGENCY JUMP!"
170    ? "    5 - SIGNAL SURRENDER!\X11"
175    INPUT "  YOUR COMMAND"; CM$ :CM = VAL(CM$)
180    IF CM < 1 OR CM > 5 GOTO 175
185    ? CHR$(147);
190    A1 = 30 :A2 = 30 :A3 = 30
195    IF CM = 1 THEN A1 = 60
200    IF CM = 2 THEN A2 = 60
205    IF CM = 3 THEN A3 = 60
210    IF CM = 4 THEN GOSUB 1135
215    IF CM = 5 GOTO 1255
220    GOSUB 270  :INPUT "   PRESS <RETURN>"; YN$
225    GOSUB 560  :INPUT "   PRESS <RETURN>"; YN$
230    GOSUB 660 :INPUT "   PRESS <RETURN>"; YN$
235    GOSUB 475
240    GOSUB 420
245    A0 = A0 + 1
250 GOTO 140
255 REM
260 REM -------------------- BATTLE ACTION -----------------
265 REM
270 ? HR$ "  BATTLE REPORT" :?HR$;
275 FOR X = 6 TO A9
280    Q=0
285    FOR P = 1 TO A8(X)
290       Q = Q + INT(RND(1)*6)
295    NEXT
300    IF Q > 8 GOTO 335
305    D = INT(RND(1)*A1)
310    ? "   TURRET" X-5;
315    IF D < 11 THEN ? "NICKS THE ENEMY SHIP."
320    IF D > 10 AND D < 21 THEN ? "HITS THE ENEMY SHIP!"
325    IF D > 20 THEN ? "WALLOPS THE ENEMY SHIP!!"
330    A5 = A5 + D
335 NEXT
340 ?
345 FOR X = 1 TO A4
350    IF INT(RND(1)*6)+INT(RND(1)*6)+2 > 6 GOTO 390
355    T = INT(RND(1)*A9)
360    ? "   OUR " A6$(T) " IS HIT."
365    A8(T) = A8(T)+1
370    IF INT(RND(1)*6)+INT(RND(1)*6)+2 > 6 GOTO 390
375    C = INT(RND(1)*B2)
380    ? "   " B0$(C) " IS HIT."
385    B1(C) = B1(C) + 1
390 NEXT 
395 ?
400 RETURN
405 REM
410 REM --------------- CHECK ENEMY STATUS --------------
415 REM
420 IF A5 < A4 * 100 THEN RETURN
425 IF A5 - A4 > 100 GOTO 440
430 ? HR$ "  SENSOP'S REPORT" :? HR$;
435 ? "   ZE ENEMY RUNS!  VE HAVE VON!!" :END
440 ? HR$ "  HELM REPORT" :?HR$
445 ? "   THE ENEMY SURRENDERS!"
450 ? "   WE HAVE CLAIMED SALVAGE RIGHTS TO THEIR" A4*100 "TON CORSAIR!"
455 END
460 REM
465 REM --------------- CHECK POWERPLANT STATUS --------------
470 REM
475 IF A8(4) = A7(4)+1 GOTO 525
480 IF A8(4) <= A7(4)+1 THEN RETURN
485 IF B1(2) > 1 GOTO 510
490 ? HR$ "  ENGINEER'S REPORT" :?HR$;
495 ? "  ACH CAPTAIN, THE POWERRR PLANT'S A COMPLETE WRRRECK!"
500 ? "  WE'RE DEAD IN TH' WATERRR!"
505 GOTO 1270
510 ? HR$ "  HELM REPORT" :?HR$;
515 ? "  CAPTAIN, THE ENGINES AREN'T RESPONDING. THE SHIP APPEARS TO BE DEAD.\X11"
520 GOTO 1270
525 ? HR$ "  ENGINEER'S REPORT" :?HR$;
530 ? "  ACH CAPTAIN, THE POWERRR PLANT'S A MESS."
535 ? "  ONE MORE HIT LIKE THAT AN' WE'RRRE FINISHED!"
540 RETURN
545 REM
550 REM -------------------- REPAIR ACTION -----------------
555 REM
560 ? HR$ "  DAMAGE CONTROL REPORT" :?HR$;
565 IF B1(2) > 1 THEN ? "  THE ENGINEER IS HURT; NO REPORT AVAILABLE.\X11" :RETURN
570 ? "", "COMPONENT",, "STATUS"
575 ? HW$
580 FOR X = 0 TO A9
585    IF A8(X) > 0 AND INT(RND(1)*A2) > 19 THEN A8(X) = A8(X) - 1
590    IF X = 5 THEN ? DL$;
595    ? "   ",
600    ? LEFT$(A6$(X)+"             ",16),
605    A = A7(X)
610    B = A8(X)
615    IF B>0 AND B>A AND X<6 THEN ? "   " + CHR$(28) + "OUT OF ACTION" + CHR$(155)
620    IF B>0 AND (B<=A OR X>5) THEN ? "   " + CHR$(158) + "DAMAGED" + CHR$(155)
625    IF B=0 THEN ? "   " + CHR$(153) + "OK" + CHR$(155)
630 NEXT
635 ?
640 RETURN
645 REM
650 REM -------------------- MEDICAL ACTION -----------------
655 REM
660 ? HR$ "  MEDICAL REPORT" :?HR$;
665 IF B1(3) > 1 THEN ? "  THE MEDIC IS HURT; NO REPORT AVAILABLE.\X11" :RETURN
670 ? "", "CREWMEMBER", "ROLE      ", "WOUNDS"
675 ? HW$
680 FOR X = 0 TO B2
685    IF B1(X) > 0 AND INT(RND(1)*A3) > 19 THEN B1(X)=B1(X)-1
690    IF B1(X) > 0 AND INT(RND(1)*A3) > 29 THEN B1(X)=B1(X)-1
695    IF B1(X) > 0 AND INT(RND(1)*A3) > 39 THEN B1(X)=B1(X)-1
700    IF X = 4 THEN ? DL$;
705    ? "   ",
710    ? LEFT$(B0$(X)+"            ",16),
715    R$ = "          "
720    IF X = 0 THEN R$ = "PILOT     "
725    IF X = 1 THEN R$ = "ASTROGATOR"
730    IF X = 2 THEN R$ = "ENGINEER  "
735    IF X = 3 THEN R$ = "MEDIC     "
740    IF X = 4 THEN R$ = "STEWARD   "
745    ? R$,
750    W$ = CHR$(28) + "OUT OF ACTION" + CHR$(155)
755    IF B1(X) = 0 THEN W$ = CHR$(153) + "OK" + CHR$(155)
760    IF B1(X) = 1 THEN W$ = CHR$(158) + "WOUNDED" + CHR$(155)
765    ? W$
770 NEXT
775 ?
780 RETURN
785 REM 
790 REM ---------------- INITIALIZE CREW ----------------
795 REM
800 REM  CREW ROLES ARE INDIVIDUALIZED AS FOLLOWS:
805 REM
810 REM    0 - PILOT
815 REM    1 - ASTROGATOR
820 REM    2 - ENGINEER
825 REM    3 - MEDIC
830 REM    4 - STEWARD/FREIGHTMASTER
835 REM    5 AND UP - GUNNERS
840 REM
845 DIM B0$(24), B1(24)
850 B2 = -1
855    B2 = B2+1
860    READ B0$(B2)
865    B1(B2) = 0
870    IF B0$(B2) <> "-1" GOTO 855
875 B2 = B2 - 1
880 DATA JAMISON, AKRIND, KODRAY, ARRLANROUGHL, REYNALDI, SHARIK, -1
885 RETURN
890 REM
895 REM ----------------- INITIALIZE SHIP ------------------
900 REM
905 REM   "SUBTYPE" IS DRIVE RATING, WEAPON SUBTYPE, OR FUEL AMOUNT.
910 REM
915 REM   COMPONENT 0 IS "THE HULL".
920 REM   COMPONENT 1 IS STREAMLINING, GENERALLY.
925 REM   COMPONENT 2 IS THE MANEUVER DRIVE.
930 REM   COMPONENT 3 IS THE JUMP DRIVE.
935 REM   COMPONENT 4 IS THE POWER PLANT.
940 REM   COMPONENT 5 IS FUEL TANKAGE.
945 REM   COMPONENT 6 AND UP ARE DEFENSES.
950 REM
955 DIM A6$(40), A7(40), A8(40)
960 A9=-1
965    A9=A9+1
970    READ A6$(A9)
975    READ A7(A9)
980    A8(A9) = 0
985    IF A6$(A9) <> "-1" GOTO 965
990 A9 = A9 - 1
995 DATA HULL,         	0 
1000 DATA STREAMLINING, 	0 
1005 DATA MANEUVER DRIVE,    1 
1010 DATA JUMP DRIVE,        1 
1015 DATA POWER PLANT,       1 
1020 DATA FUEL TANKAGE,      10
1025 DATA TURRET 1,      	2
1030 DATA TURRET 2,     	2
1035 REM DATA TURRET 1L,   21,   TURRET 2L,  22,  TURRET 3L,  23
1040 REM DATA TURRET 1M,   31,   TURRET 2M,  32,  TURRET 3M,  33
1045 REM DATA TURRET 1S,   41,   TURRET 2S,  42,  TURRET 3S,  43
1050 REM
1055 REM DATA BARBETTE L,  53,   BAY L,      54
1060 REM DATA BARBETTE M,  63,   BAY M,      64
1065 REM DATA BARBETTE S,  73,   BAY S,      74
1070 REM DATA BARBETTE A,  83,   BAY A,      84
1075 DATA -1, -1
1080 RETURN
1085 REM
1090 REM ------------------- INITIALIZE ENEMY ------------------
1095 REM
1100 REM  FOR NOW, LET'S JUST USE A SIMPLE DAMAGE LEVEL
1105 A4 = INT(RND(1)*8+2) :REM 200 TO 900 TONS
1110 A5 = 0
1115 RETURN
1120 REM
1125 REM ------------------ EMERGENCY JUMP --------------------
1130 REM
1135 IF B1(1) > 0 GOTO 1175
1140 IF B1(2) > 0 GOTO 1190
1145 IF A8(3) >= A7(3) GOTO 1220
1150 IF INT(RND(1)*6)+INT(RND(1)*6)+2 > 6 GOTO 1205
1155 ? "   AYE, YOU'VE GOT IT, CAPTAIN!"
1160 FOR T = 1 TO 1000 : TT = SQR(12345) :NEXT
1165 ? "   CAPTAIN, WE'VE SUCCESSFULLY JUMPED TO SAFETY!"
1170 END
1175 ? HR$ "   HELM REPORT" :? HR$;
1180 ? "   OUR ASTROGATOR IS WOUNDED, AND CANNOT CALCULATE A JUMP.\X11"
1185 RETURN
1190 ? HR$ "   HELM REPORT" :? HR$;
1195 ? "   OUR ENGINEER IS WOUNDED, AND CANNOT OPERATE THE JUMP DRIVE.\X11"
1200 RETURN
1205 ? HR$ "   ENGINEER'S REPORT" :? HR$;
1210 ? "   ACH, WE KENNA JUMP YET CAPTAIN, GIMME 20 MORRRE MINUTES!\X11"
1215 RETURN
1220 ? HR$ "   ENGINEER'S REPORT" :? HR$;
1225 ? "   ACH, THE JUMP DRIVES ARRRE DOON, CAPTAIN!"
1230 ? "   WE NE KENNA JUMP UNTIL THEY'RRRE REPAIRRRED!\X11"
1235 RETURN
1240 REM
1245 REM --------------------- SURRENDER ----------------------
1250 REM
1255 ? HR$ "  HELM REPORT": ?HR$;
1260 ? "  SURRENDER ACKNOWLEDGED. CREW IS ARMING THEMSELVES.\X11"
1265 FOR T = 1 TO 1000: TT=SQR(12345) :NEXT
1270 ? HR$ "  HELM REPORT": ?HR$;
1275 ? "  CAPTAIN, WE HAVE BEEN BOARDED."
1280 FOR T = 1 TO 1000: TT=SQR(12345) :NEXT
1285 ? "  FIGHTING REPORTED IN ALL AREAS."
1290 FOR T = 1 TO 1000: TT=SQR(12345) :NEXT
1295 RR = INT(RND(1)*6)+INT(RND(1)*6)+2
1300 IF RR < 6 THEN ? "  WE HAVE REPELLED THE PIRATES!!" :END
1305 IF RR < 8 THEN ? "  OUR CARGO AND VEHICLES HAVE BEEN TAKEN." :END
1310 IF RR < 10 THEN ? "  WE HAVE BEEN STRANDED ON A MARGINAL WORLD." :END
1315 ? "  THE CREW HAS BEEN SOLD INTO SLAVERY."
1320 END
